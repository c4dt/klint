#ifndef GHOST_MAP_GH_INCLUDED
#define GHOST_MAP_GH_INCLUDED

// -- Public API --

fixpoint option<v> ghostmap_get<k, v>(list<pair<k, v> > ghostmap, k key) {
  switch(ghostmap) {
    case nil: return none;
    case cons(h, t):
      return switch(h) {
        case pair(hk, hv):
          return hk == key ? some(hv) : ghostmap_get(t, key);
      };
  }
}

fixpoint list<pair<k, v> > ghostmap_remove<k, v>(list<pair<k, v> > ghostmap, k key) {
  switch(ghostmap) {
    case nil: return nil;
    case cons(h, t):
      return switch(h) {
        case pair(hk, hv):
          return hk == key ? ghostmap_remove(t, key) : cons(h, ghostmap_remove(t, key));
      };
  }
}

fixpoint list<pair<k, v> > ghostmap_set<k, v>(list<pair<k, v> > ghostmap, k key, v value) {
  return cons(pair(key, value), ghostmap_remove(ghostmap, key));
}

// -- Private API --

fixpoint bool ghostmap_distinct<k, v>(list<pair<k, v> > ghostmap) {
  return distinct(map(fst, ghostmap));
}

// -- Lemmas --

lemma void ghostmap_remove_preserves_absence<k, v>(list<pair<k, v> > ghostmap, k key, k absent_key)
requires ghostmap_get(ghostmap, absent_key) == none &*&
         absent_key != key;
ensures ghostmap_get(ghostmap_remove(ghostmap, key), absent_key) == none;
{
  switch(ghostmap) {
    case nil:
    case cons(h, t):
      switch(h) {
        case pair(hk, hv):
          ghostmap_remove_preserves_absence(t, key, absent_key);
      }
  }
}

// -- Auto lemmas --

lemma_auto(ghostmap_get(ghostmap, key)) void ghostmap_get_implies_mem1<k, v>(list<pair<k, v> > ghostmap, k key)
requires ghostmap_get(ghostmap, key) != none;
ensures true == mem(key, map(fst, ghostmap));
{
  switch(ghostmap) {
    case nil:
    case cons(h, t):
      switch(h) {
        case pair(hk, hv):
          if (hk != key) {
            ghostmap_get_implies_mem1(t, key);
          }
      }
  }
}

lemma_auto(ghostmap_get(ghostmap, key)) void ghostmap_get_implies_mem2<k, v>(list<pair<k, v> > ghostmap, k key)
requires ghostmap_get(ghostmap, key) == none;
ensures false == mem(key, map(fst, ghostmap));
{
  switch(ghostmap) {
    case nil:
    case cons(h, t):
      switch(h) {
        case pair(hk, hv):
          if (hk != key) {
            ghostmap_get_implies_mem2(t, key);
          }
      }
  }
}

lemma_auto(ghostmap_remove(ghostmap, key)) void ghostmap_remove_implies_notmem<k, v>(list<pair<k, v> > ghostmap, k key)
requires true;
ensures false == mem(key, map(fst, ghostmap_remove(ghostmap, key)));
{
  switch(ghostmap) {
    case nil:
    case cons(h, t):
      switch(h) {
        case pair(hk, hv):
          ghostmap_remove_implies_notmem(t, key);
      }
  }
}

lemma_auto(ghostmap_remove(ghostmap, key)) void ghostmap_remove_preserves_distinct<k, v>(list<pair<k, v> > ghostmap, k key)
requires true == ghostmap_distinct(ghostmap);
ensures true == ghostmap_distinct(ghostmap_remove(ghostmap, key));
{
  switch(ghostmap) {
    case nil:
    case cons(h, t):
      switch(h) {
        case pair(hk, hv):
          ghostmap_remove_preserves_distinct(t, key);
          if (hk != key) {
            ghostmap_remove_preserves_absence(t, key, hk);
          }
      }
  }
}

lemma_auto(ghostmap_set(ghostmap, key, value)) void ghostmap_set_preserves_distinct<k, v>(list<pair<k, v> > ghostmap, k key, v value)
requires true == ghostmap_distinct(ghostmap);
ensures true == ghostmap_distinct(ghostmap_set(ghostmap, key, value));
{
  switch(ghostmap) {
    case nil:
    case cons(h, t):
      switch(h) {
        case pair(hk, hv):
          if (hk != key) {
            ghostmap_set_preserves_distinct(t, key, value);
          }
      }
  }
}

lemma_auto(ghostmap_remove(ghostmap, key)) void ghostmap_remove_unchanged_when_missing<k, v>(list<pair<k, v> > ghostmap, k key)
requires ghostmap_get(ghostmap, key) == none;
ensures ghostmap_remove(ghostmap, key) == ghostmap;
{
  switch(ghostmap) {
    case nil:
    case cons(h, t):
      switch(h) {
        case pair(hk, hv):
          if (hk != key) {
            ghostmap_remove_unchanged_when_missing(t, key);
          }
      }
  }
}

#endif
